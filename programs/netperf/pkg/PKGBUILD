pkgname=netperf
pkgver=git
pkgrel=0
arch=('i386' 'x86_64' 'riscv64' 'aarch64')
url="https://github.com/HewlettPackard/netperf"
license=('GPL')
source=("https://github.com/HewlettPackard/netperf.git")
md5sums=('SKIP')

. $LKP_SRC/lib/tests/pkgbuild.sh

prepare()
{
	prepare_benchmark_path
}

build()
{
	cd_src_pkg_dir

	if [[ $CARCH == 'riscv64' ]]; then
		. $LKP_SRC/lib/reproduce-log.sh
		log_cmd wget https://git.savannah.gnu.org/cgit/config.git/plain/config.guess -O config.guess
		log_cmd wget https://git.savannah.gnu.org/cgit/config.git/plain/config.sub -O config.sub
	fi

	# sendfile_tcp_stream() misses assignment of len and it is possible
	# for random len to trigger the following issue, so fix it.
	# stderr: netperf: data send error: sendfile: No space left on device
	# stderr: len was -1847701200 send_size was 5120
	sed -i 's/(netperf_sendfile/(len = netperf_sendfile/g' src/nettest_bsd.c

	local configure_flags=(
				--prefix=$benchmark_path
				--enable-demo
				--enable-unixdomain
				--enable-sctp
				--enable-dccp
				--enable-burst
	)

	./autogen.sh
	CFLAGS="-std=gnu89 -fcommon" ./configure "${configure_flags[@]}"
	make
}

package()
{
	cd_src_pkg_dir

	make install
}
